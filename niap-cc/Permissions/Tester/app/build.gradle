/*
 * Copyright (C) 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
def homePath = System.properties['user.home']
def applicationName = 'Tester'
android {
    signingConfigs {
        // TODO :
        // Prepare your own normal.jks and platform.jks as settings and put these into
        // assigned path. It helps to build the application variants automatically.
        // For details see the instructions on the SIGNING.md
        norm{
            storeFile file("../security/normal/normal.jks")
            storePassword "android"
            keyPassword "android"
            keyAlias "normal"
        }
        platform {
            storeFile file('../security/platform/platform.jks')
            storePassword "android"
            keyPassword "android"
            keyAlias "platform"
        }
        debugkeystore {
            storeFile file(homePath+'/.android/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def flavor = variant.productFlavors.name[0]
            outputFileName = "${applicationName}-${flavor}-${variant.buildType.name}.apk"
        }
    }

    compileSdkVersion 33
    defaultConfig {
        applicationId "com.android.certifications.niap.permissions"
        minSdkVersion 28
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            signingConfig null
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig null
            debuggable true
        }
    }
    flavorDimensions "settings"
    productFlavors {
        normal {
            dimension "settings"
            signingConfig signingConfigs.norm
        }
        noperm {
            dimension "settings"
            signingConfig signingConfigs.norm
        }
        platform {
            dimension "settings"
            signingConfig signingConfigs.platform
        }
    }
    compileOptions {
        targetCompatibility = "11"
        sourceCompatibility = "11"
    }
    lint {
        abortOnError false
    }
    namespace 'com.android.certifications.niap.permissions'

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.gms:play-services-location:20.0.0'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.android.gms:play-services-base:18.1.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //implementation 'androidx.work:work-runtime:2.7.1'
    implementation "com.google.guava:guava:27.0.1-android"
    implementation 'com.google.code.gson:gson:2.8.6'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

}
